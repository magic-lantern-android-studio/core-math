apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 26
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'overview-summary.html'
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

// Task to delete the old jar
task deleteOldJar(type: Delete){
    delete 'release/mlmath.jar'
}

// Task to export contents as jar
task exportJar(type: Copy){
    from('build/intermediates/aar_main_jar/release/')
    into('release/')
    include('classes.jar')
    // Rename the jar.
    rename('classes.jar', 'mlmath.jar')
}

exportJar.dependsOn(deleteOldJar, build)

// Task to generate jar containing sources
task generateSourcesJar (type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

// Task to delete the old jar
task deleteSourcesJar(type: Delete){
    delete 'libs/mlmath-sources.jar'
}

task exportSourcesJar(type: Copy){
    from('build/libs/')
    into('release/')
    include('app-sources.jar')
    // Rename the jar.
    rename('app-sources.jar', 'mlmath-sources.jar')
}

exportSourcesJar.dependsOn(deleteSourcesJar, generateSourcesJar)

artifacts {
    archives generateSourcesJar
}